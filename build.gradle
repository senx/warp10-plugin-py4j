//
//   Copyright 2019  SenX S.A.S.
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.
//

// Apply the java plugin to add support for Java
plugins {
    id 'java'
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'idea'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.1'
}

// ------------------------------------------------------------------
//                    AUTO RELEASE MANAGEMENT
// Gets the version name from the latest Git tag
// if no commit occurs -> the name of the tag likes 0.0.1
// if commit occurs -> 0.0.1-12-aabb1122 (number of commits + version number)
// ------------------------------------------------------------------
def getVersionName(String revision) {
  String newRevision = revision

  if (!revision.endsWith('SNAPSHOT')) {
    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'describe', '--tags', '--always'
      standardOutput = stdout
    }
    newRevision = stdout.toString().trim()
  }
  return newRevision
}

project.ext.bintrayRepo = 'maven'
project.ext.bintrayVcsUrl = 'https://github.com/senx/warp10-plugin-py4j'
project.ext.bintrayLicenses = [ 'Apache-2.0' ]
project.ext.group = 'io.warp10'
project.ext.name = 'warp10-plugin-py4j'
project.ext.uberjar = null != System.getProperty('uberjar')
project.ext.version = getVersionName('1.0.0') + (project.ext.uberjar ? '-uberjar' : '')
version project.ext.version

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

// In this section you declare where to find the dependencies of your project
repositories {
    // flatDir name:'ExternalJars', dirs:'./lib'
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    
    maven {
      url 'https://dl.bintray.com/senx/maven'
    }
    
    maven {
      url 'https://dl.bintray.com/hbs/maven'
    }

    maven {
      url 'https://bintray.com/yahoo/maven'
    }
        
    maven {
      url "http://nexus.bedatadriven.com/content/groups/public/"
    }
    
    maven {
      url "http://repo.opengeo.org/"
    }
        
    maven {
      url 'https://repository.apache.org/content/groups/public'
    }

    mavenCentral()
    
    mavenLocal()
    
    maven {
      url "http://maven.twttr.com"
    }
}

configurations {
    provided
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    provided 'org.slf4j:slf4j-api:1.7.13'
    provided 'io.warp10:warpscript:2.2.0'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.

    testCompile 'junit:junit:4.12'

    // https://mvnrepository.com/artifact/net.sf.py4j/py4j
    compile group: 'net.sf.py4j', name: 'py4j', version: '0.10.8.1'
}

sourceSets {
  main {
    compileClasspath = compileClasspath + configurations.provided
  }
}

// for IntelliJ users
idea {
  module {
    scopes.PROVIDED.plus += [configurations.provided]
  }
}

// for Eclipse users
eclipse {
  classpath {
    plusConfigurations += [ configurations.provided ]
  }
}

jar {
  from('src/main/warpscript') {
    //
    // Only consider macros which are in our namespace
    //
    include project.ext.group + '/' + project.ext.name + '/*.mc2'
    include project.ext.group + '/' + project.ext.name + '/**/*.mc2'
  }
  from('.') {
    include 'README.md'
    include 'io.warp10-warp10-plugin-py4j.conf'
  }
}

shadowJar {
  zip64 = true
  classifier = ''
  manifest {
    attributes("Implementation-Title": "Warp 10 Py4J Plugin")
    attributes("Implementation-Version": project.ext.version)
    attributes('WarpFleet-Description': 'Interacting with Warp 10™ from Python with Py4J')
    attributes('WarpFleet-Group': project.ext.group )
    attributes('WarpFleet-Artifact': project.ext.name)
    attributes('WarpFleet-Version': project.ext.version)
  }

  from('src/main/warpscript') {
    //
    // Only consider macros which are in our namespace
    //
    include project.ext.group + '/' + project.ext.name + '/*.mc2'
    include project.ext.group + '/' + project.ext.name + '/**/*.mc2'
  }
  from('.') {
    include 'README.md'
    include 'io.warp10-warp10-plugin-py4j.conf'
  }

  dependencies {
    exclude(dependency('io.warp10:warpscript:'))
    exclude(dependency('org.slf4j::'))
  }
}

shadowJar.dependsOn(test)

task shadowJarFull(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
  zip64 = true
  group = 'Shadow'
  classifier = 'full'
  from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
  configurations = [project.configurations.provided ]//,project.configurations.compile]
  manifest {
    attributes("Implementation-Title": "Warp 10 Py4j Plugin")
    attributes("Implementation-Version": "0.0.1")
  }
}

// Task for building the sources jar
task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compile
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
}

javadocJar.dependsOn(javadoc)

publishing {
  publications {
    //
    // Publication for the standard jar
    //

    stdjar(MavenPublication) {
      groupId project.ext.group
      artifactId project.ext.name
      version project.ext.version

      // We publish the result of the 'jar' task and the sources jar
      from components.java 
      artifact sourcesJar
      artifact javadocJar
    }

    //
    // Publication for the über jar containing all dependencies
    //

    uberjar(MavenPublication) {
      groupId project.ext.group
      artifactId project.ext.name
      version project.ext.version

      // Publish the über jar and the sources
      artifact sourcesJar
      artifact shadowJar
    }
  }
}

bintray {
  dryRun = false

  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

  publications = project.ext.uberjar ? [ 'uberjar' ] : ['stdjar' ]
  publish = null != System.getProperty('publish')

  pkg {

    repo = project.ext.bintrayRepo
    name = project.ext.name

    if (project.hasProperty('bintrayUserOrg') || null != System.getenv('BINTRAY_USERORG')) {
      userOrg = project.hasProperty('bintrayUserOrg') ? project.property('bintrayUserOrg') : System.getenv('BINTRAY_USERORG')
    }

    vcsUrl = project.ext.bintrayVcsUrl
    licenses = project.ext.bintrayLicenses

    version {
      version = version
      released = new Date()
    }
  }
}

